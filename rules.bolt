isUser(uid) { auth != null && auth.uid == uid }

withInitialCondition(ref, exp) { isNew(ref) && exp }

isNew(ref) { prior(ref) == null }

noChange(ref) { ref == prior(ref) }

type CurrentTimestamp extends Number {
  validate() { this == now }
}

type Progress extends Number {
  validate() { this >= 0 && this <= 100 }
}

type StateTimestamp extends Number {
  validate() { prior(this).parent() == null ? this == now : prior(this) == this }
}

type InitialTimestamp extends Number {
  validate() { noChange(this) || withInitialCondition(this, this == now) }
}

type PositiveNumber extends Number {
  validate() { this >= 0 }
}

type Percent extends Number {
  validate() { this <= 100 }
}

type WholeNumber extends Number {
  validate() { this >= 0 }
}

type Device {
  validate() { this.gcmId == key() && (this.type == 'ios' || this.type == 'android' || this.type == 'web') }
  name: String,
  added: StateTimestamp,
  lastActive: CurrentTimestamp,
  gcmId: String,
  type: String
}

type User {
  cur_group: String | Null,
  cur_group_visible: Boolean | Null,
  email: String,
  product: String,
  type: String,
  devices: Map<String, Device>,
  friends: Map<String, Boolean>
}

type Track {
  added: Number,
  album: String,
  albumArtSmall: String | Null,
  albumArtMedium: String | Null,
  albumArtLarge: String | Null,
  artist: String,
  key: String,
  lengthMs: Number,
  songId: String,
  title: String,
  uri: String
  votedUp: Map<String, Boolean>,
  votedDown: Map<String, Boolean>
}

type Participant {
  country: String,
  online: Boolean | Null,
  display_name: String | Null,
  id: String,
  ext_url: String,
  uri: String,
  image_url: String
}

type Security {
  validate() { this.type == 'invite' || this.type == 'password' }
  type: String,
  password: String | Null
}

type Queuegroup {
  validate() { this.name == key() }
  created: CurrentTimestamp,
  leader: String,
  name: String,
  public: Boolean | Null,
  security: Security | Null,
  participants: Map<String, Boolean|String>,
  invites: Map<String, Boolean>,
  tracks: Map<String, Track>,
  pastTracks: Map<String, Track>,
  nowPlaying: Track | Null
}

type ErrorDetails {
  error: String,
  error_stack: String,
  previous_state: String,
  original_task: Any,
  attempts: WholeNumber
}

type QueueInvite {
  _state: String | Null,
  _state_changed: InitialTimestamp | Null,
  _owner: String | Null,
  _progress: Percent | Null,
  _error_details: ErrorDetails | Null,
  _id: String | Null,
  groupName: String,
  inviter: String,
  invitee: String
}

type QueueSpec {
  start_state: String | Null,
  in_progress_state: String,
  finished_state: String | Null,
  error_state: String | Null,
  timeout: WholeNumber | Null
}

path /queue/invites/{$queueInvite} is QueueInvite {
  index() {["_state"]}
  read() { auth.canProcessTasks == true }
  create() { auth != null }
  update() { auth.canProcessTasks == true }
  delete() { auth.canProcessTasks == true }
}

path /display_names/{$displayName} {
  read() { auth != null }
}

path /display_names/{$displayName}/{$friendCode} {
  read() { auth != null }
  write() { isUser(this) }
}

path /participants/{$userId} is Participant {
  read() { auth != null }
  write() { isUser($userId) }
}

path /users/{$userId} is User {
  read() { isUser($userId) }
  write() { isUser($userId) }
}

path /users/{$userId}/cur_group {
  read() { auth != null && this.parent().cur_group_visible }
}

path /users/{$userId}/devices {
  read() { auth.canProcessTasks == true }
}

path /queuegroups/{$id} is Queuegroup {
  create() { auth != null }
  update() { auth.uid==prior(root.queuegroups[$id].leader) }
  delete() { auth.uid==prior(root.queuegroups[$id].leader) }
  read() { root.queuegroups[$id].participants[auth.uid] != null }
}

path /queuegroups/{$id}/leader {
  write() { auth.uid==prior(root.queuegroups[$id].leader) }
}

path /queuegroups/{$id}/name {
  write() { auth.uid==prior(root.queuegroups[$id].leader) }
  read() { auth != null }
}

path /queuegroups/{$id}/tracks {
  write() { prior(root.queuegroups[$id].participants[auth.uid]) != null }
}

path /queuegroups/{$id}/invites {
  write() { prior(root.queuegroups[$id].participants[auth.uid]) != null }
  read() { auth != null }
}

path /queuegroups/{$id}/nowPlaying {
  write() { true }
  read() { this.parent().public == true || prior(root.queuegroups[$id].participants[auth.uid]) != null }
}

path /queuegroups/{$id}/pastTracks {
  write() { auth.uid==prior(root.queuegroups[$id].leader) }
}

path /queuegroups/{$id}/participants/{$key3} {
  create() { key()==auth.uid &&
            (prior(root.queuegroups[$id].security) == null ?
              true : (prior(root.queuegroups[$id].security.type) == 'invite' ?
                prior(root.queuegroups[$id].invites[key()]) != null : (prior(root.queuegroups[$id].security.type) == 'password' ?
                  this == prior(root.queuegroups[$id].security.password) : false)))}
  update() { key()==auth.uid || auth.uid==prior(root.queuegroups[$id].leader) }
  delete() { key()==auth.uid || auth.uid==prior(root.queuegroups[$id].leader) }
  read() { this.parent().public == true || prior(root.queuegroups[$id].participants[auth.uid]) != null }
}
